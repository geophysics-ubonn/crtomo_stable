        subroutine bsmatm()

c Unterprogramm belegt die Rauhigkeitsmatrix (nur fuer regulaere Grids).

c Andreas Kemna                                            29-Feb-1996
c                                       Letzte Aenderung   20-Mar-2003

c.....................................................................

        USE alloci

        INCLUDE 'parmax.fin'
        INCLUDE 'elem.fin'
        INCLUDE 'dat.fin'
        INCLUDE 'model.fin'
        INCLUDE 'konv.fin'
        INCLUDE 'inv.fin'

c.....................................................................

c PROGRAMMINTERNE PARAMETER:

c Hilfsvariablen
        real            * 8     dum,dx(mmax),dz(mmax)
        integer         * 4     i,j,k2
      
c Hilfsfunction
        integer         * 4     k

        k(i,j) = (i-1) * nx + j

c.....................................................................

c Rauhigkeitsmatrix auf Null setzen
        do i=1,manz
            do j=1,3
                smatm(i,j) = 0d0
            end do
        end do

c Inkrementfelder belegen
        do i=1,nz
          k2    = k(i,1)
          dz(i) = sy(snr(nrel(k2,4)))-sy(snr(nrel(k2,1)))
        end do

        do j=1,nx
          k2    = k(1,j)
          dx(j) = sx(snr(nrel(k2,3)))-sx(snr(nrel(k2,4)))
        end do

c Rauhigkeitsmatrix belegen
        do i=1,nz
          do j=1,nx
              k2 = k(i,j)

              if (j.gt.1) then
                  dum         = alfx*dz(i)*2d0/(dx(j-1)+dx(j))
                  smatm(k2,1) = dum
              end if

              if (j.lt.nx) then
                  dum         = alfx*dz(i)*2d0/(dx(j)+dx(j+1))
                  smatm(k2,1) = smatm(k2,1) + dum
                  smatm(k2,2) = -dum
              end if

              if (i.gt.1) then
                  dum         = alfz*dx(j)*2d0/(dz(i-1)+dz(i))
                  smatm(k2,1) = smatm(k2,1) + dum
              end if

              if (i.lt.nz) then
                  dum         = alfz*dx(j)*2d0/(dz(i)+dz(i+1))
                  smatm(k2,1) = smatm(k2,1) + dum
                  smatm(k2,3) = -dum
              end if
          end do
        end do

        return
        end
